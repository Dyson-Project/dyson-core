buildscript {
    apply from: "$rootDir/gradle/dependencies.gradle"
}
plugins {
    id 'java-library'
    id 'maven-publish'
    id 'io.spring.dependency-management' version '1.1.3'
}
ext {
    mavenUrl = project.hasProperty('mavenUrl') ? project.mavenUrl : ''
    mavenUsername = project.hasProperty('mavenUsername') ? project.mavenUsername : ''
    mavenPassword = project.hasProperty('mavenPassword') ? project.mavenPassword : ''
}
allprojects {
    group = "org.dyson"
    version = "0.0.1-SNAPSHOT"
    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
        maven { url 'https://repo.spring.io/snapshot' }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:$versions.springBootVersion"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:$versions.springCloudVersion"
            mavenBom "org.axonframework:axon-bom:$versions.axonVersion"
        }
    }
    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }
    publishing {
        repositories {
            maven {
                name = "reposilite"
                url = mavenUrl
                credentials(PasswordCredentials)
                allowInsecureProtocol = true
            }
        }
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
    }
}
project(":core") {
    archivesBaseName = "dyson-core"
    dependencies {
        implementation libs.axonModelling
        implementation libs.grpcProtobuf
        implementation libs.jakartaServletApi
        implementation libs.jakartaAnnotationApi
        implementation libs.jakartaPersistenceApi
        implementation libs.springStarterValidation
        implementation libs.jacksonDatabind
        implementation libs.springSecurityCore
        implementation libs.springWeb
        implementation libs.springWebMvc
        implementation libs.springDataCommons

        compileOnly libs.lombok
        testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
        // This dependency is exported to consumers, that is to say found on their compile classpath.
        api 'org.apache.commons:commons-math3:3.6.1'
        // This dependency is used internally, and not exposed to consumers on their own compile classpath.
        implementation 'com.google.guava:guava:30.1.1-jre'
    }

}
project(":domain") {
    archivesBaseName = "dyson-domain"
    dependencies {
        implementation libs.jakartaPersistenceApi
        implementation libs.jakartaValidationApi
        implementation libs.jakartaAnnotationApi
        implementation libs.apacheCommonLang

        implementation project(":core")

        compileOnly libs.lombok
        annotationProcessor libs.lombok
        testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    }
}
project(":security") {
    archivesBaseName = "dyson-security"
    dependencies {
        compileOnly libs.jakartaServletApi
        implementation libs.springSecurityOauth2Jose

        implementation libs.jakartaPersistenceApi
        implementation libs.jakartaValidationApi
        implementation libs.jakartaAnnotationApi
        implementation libs.apacheCommonLang

        implementation project(":core")

        compileOnly libs.lombok
        testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    }

}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}
